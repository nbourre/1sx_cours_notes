# Les capteurs <!-- omit in toc -->

# Table des matiÃ¨res <!-- omit in toc -->
- [Capteur de distance](#capteur-de-distance)
    - [Le sonar Ã  ultrason](#le-sonar-Ã -ultrason)
    - [Le sonar HC-SR04](#le-sonar-hc-sr04)
    - [Limites et prÃ©cautions du HC-SR04](#limites-et-prÃ©cautions-du-hc-sr04)
    - [Algorithme](#algorithme)
    - [Exemple de code](#exemple-de-code)
- [Importation de bibliothÃ¨que](#importation-de-bibliothÃ¨que)
- [Capteur Ã  tempÃ©rature et humiditÃ©](#capteur-Ã -tempÃ©rature-et-humiditÃ©)
  - [DHT11 - SpÃ©cifications](#dht11---spÃ©cifications)
  - [DHT11 - Librairie](#dht11---librairie)
  - [DHT11 - Branchement et code](#dht11---branchement-et-code)
- [Ã‰cran LCD](#Ã©cran-lcd)
  - [Dans le kit](#dans-le-kit)
  - [LCD - Code](#lcd---code)
- [Exercices](#exercices)
  - [Sonar sans librairie](#sonar-sans-librairie)
  - [Sonar avec librairie](#sonar-avec-librairie)
  - [Sonar, DHT11 et LCD](#sonar-dht11-et-lcd)
- [RÃ©fÃ©rences](#rÃ©fÃ©rences)

---

# Capteur de distance
- Sans s'en rendre compte, dans la vie de tous les jours, on retrouve plusieurs appareils qui utilisent des capteurs de distance
- Voici quelques exemples d'appareils qui utilisent des capteurs de distance
  - TÃ©lÃ©phones intelligents : Lecture de proximitÃ© pour la camÃ©ra ou pour Ã©teindre l'Ã©cran
  - Drone : Distance par rapport au sol
  - Voiture : Distance par rapport Ã  un obstacle
  - Construction : Mesurer une distance

### Le sonar Ã  ultrason
- Le sonar est un appareil permettant de mesurer la distance Ã  un objet
- Il s'agit d'un capteur ultrasonique
  - Comme ce qu'utilise les chauve-souris et dauphin
- Le principe du sonar est qu'il envoie une courte impulsion ultrasonique et reÃ§oit la rÃ©ponse
- Le dÃ©lai de rÃ©ponse permet de dÃ©terminer la distance Ã  l'objet
  - La vitesse du son est d'environ 343 m/s au niveau de la mer

![Alt text](img/01_sonar.png)

---

### Le sonar HC-SR04
- Le modÃ¨le qui est inclus dans le kit est le HC-SR04 qui est relativement populaire
  - C'est le mÃªme que l'on retrouve sur le robot
- Il fonctionne entre 2 et 400 cm

![Alt text](img/02_sonar.png)

### Limites et prÃ©cautions du HC-SR04

**PortÃ©e effective :**
- Distance minimale : 2 cm (en dessous, les mesures sont imprÃ©cises)
- Distance maximale : 400 cm (au-delÃ , le signal devient trop faible)
- Zone optimale : entre 10 cm et 300 cm pour une prÃ©cision maximale

**Angle de dÃ©tection :**
- Angle de dÃ©tection d'environ 15Â° (cÃ´ne de ~30Â° total)
- Les objets en dehors de cet angle ne seront pas dÃ©tectÃ©s
- Pour une mesure prÃ©cise, l'objet doit Ãªtre perpendiculaire au capteur

**Limitations importantes :**
- Surfaces molles ou inclinÃ©es (tissus, mousse) : absorption du signal
- Surfaces trÃ¨s petites : peuvent ne pas rÃ©flÃ©chir suffisamment le signal
- TempÃ©ratures extrÃªmes : affectent la vitesse du son et donc la prÃ©cision
- Bruit ambiant : les ultrasons peuvent interfÃ©rer entre eux

**Conseils d'utilisation :**
- Ã‰vitez d'utiliser plusieurs capteurs HC-SR04 simultanÃ©ment (interfÃ©rences)
- Attendez au moins 60ms entre les mesures pour Ã©viter les Ã©chos parasites
- Filtrez les valeurs aberrantes (0 cm ou >400 cm) dans votre code

---

### Algorithme
- On active le dÃ©clencheur
- On attend quelques __micro__secondes (10)
- On dÃ©sactive le dÃ©clencheur
- On lit lâ€™Ã©cho (durÃ©e)
- On calcule la distance (cm) avec le dÃ©lai
  - `ğ‘‘ğ‘–ğ‘ ğ‘¡=ğ‘‘ğ‘¢ğ‘ŸÃ©ğ‘’ âˆ—0.034/2; // Câ€™est en microsecondes`

### Exemple de code

<table><tr><td>

```cpp
#define TRIG_PIN 3
#define ECHO_PIN 2

unsigned long currentTime;

void setup() {
  pinMode(TRIG_PIN, OUTPUT);
  pinMode(ECHO_PIN, INPUT);
  Serial.begin(115200);
  Serial.println("Capteur ultrasonique HC-SR04 Test");
}

void loop() {
  currentTime = millis();

  int distance = distanceTask(currentTime);
  printDistanceTask(currentTime, distance);
}

int distanceTask(unsigned long ct) {
  static unsigned long lastTime = 0;
  unsigned long rate = 50;
  static int lastResult = -1;
  int result = -1;

  long duration;
  int distance;

  if (ct - lastTime < rate) {
    return lastResult;
  }

  lastTime = ct;

  digitalWrite(TRIG_PIN, LOW);
  delayMicroseconds(2);

  // Activer le trigPin 10 microsecondes
  digitalWrite(TRIG_PIN, HIGH);
  delayMicroseconds(10);
  digitalWrite(TRIG_PIN, LOW);

  // Lire l'Ã©cho
  duration = pulseIn(ECHO_PIN, HIGH);
  // Calculer la distance
  distance = duration * 0.034 / 2;  // Vitesse du son / 2

  if (distance >= 2 && distance <= 400) {
    result = distance;
    lastResult = result;
  }

  return lastResult;
}

void printDistanceTask(unsigned long ct, int distance) {
  static unsigned long lastTime = 0;
  unsigned long rate = 500;

  if (ct - lastTime < rate) {
    return;
  }

  lastTime = ct;

  // Filtrer les valeurs abberantes
  if (distance >= 2 && distance <= 400) {
    Serial.print("Distance: ");
    Serial.print(distance);
    Serial.println(" cm");
  } else {
    Serial.println("Hors de portÃ©e");
  }
}

```

</td>
<td>

![Alt text](img/sonar_example.gif)

</td>
</tr>
</table>

- On remarque que j'utilise pour la dÃ©monstration la fonction `delayMicroseconds()`. Celle-ci a le mÃªme effet que `delay()`, i.e. qu'elle bloque le ÂµC. Cependant, nous sommes dans l'ordre du microseconde, ainsi ce n'est pas trÃ¨s problÃ©matique. NÃ©anmoins, pour des projets plus complexes, il est prÃ©fÃ©rable d'utiliser des techniques non bloquantes.
- Pour amÃ©liorer le code, nous allons utiliser une bibliothÃ¨que que nous allons voir dans la prochaine section.

---

# Importation de bibliothÃ¨que

- Rappel :
  - Une bibliothÃ¨que est un ensemble de fonctions qui permettent de faciliter la programmation
  - Dans Arduino, une bibliothÃ¨que est une classe qui facilite le dÃ©veloppement de projet en rÃ©utilisant le code
- Plusieurs bibliothÃ¨ques viennent par dÃ©faut avec l'environnement d'Arduino.
- Toutefois, il existe une panoplie de bibliothÃ¨ques tÃ©lÃ©chargeables pour des composants communs
- L'installation d'une bibliothÃ¨que ajoute gÃ©nÃ©ralement des exemples qui sont liÃ©s Ã  la bibliothÃ¨que tÃ©lÃ©chargÃ©es
  - Rappel : Les exemples sont dans "Fichier --> Exemples..."
   
---

- Pour tÃ©lÃ©charger une bibliothÃ¨que, il y a le "Gestionnaire de bibliothÃ¨ques"
- Il est dans le menu "Outils"
- Il y a aussi un bouton dans la barre d'outils latÃ©rale

![Alt text](img/03_ide_library_manager.png)

> *Note :* Pour les versions antÃ©rieures Ã  2.0, il y a le "Gestionnaire de bibliothÃ¨ques" dans le menu "Croquis". Une fenÃªtre surgissante s'ouvre et il faut chercher la bibliothÃ¨que Ã  installer.

---

# Capteur Ã  tempÃ©rature et humiditÃ©
- Dans le kit, il y a un capteur d'humiditÃ© et de tempÃ©rature
- Il s'agit du DHT11
- On retrouve ce type de capteur dans plusieurs situations
  - Station mÃ©tÃ©o
  - Cellulaire
  - Interrupteur de ventilation automatique
  - SystÃ¨me de suivi d'environnement

![Alt text](img/04_dht11.png)

## DHT11 - SpÃ©cifications
- CoÃ»t ultra bas
- Alimentation et E/S de 3 Ã  5V
- Utilisation d'un courant de 2,5mA maximum pendant la conversion (pendant la demande de donnÃ©es)
- Bon pour les lectures d'humiditÃ© de 20-90% avec une prÃ©cision de 5%
- Bon pour les lectures de tempÃ©rature de 0-50Â°C avec une prÃ©cision de Â±2Â°C
- Taux d'Ã©chantillonnage ne dÃ©passant pas 1 Hz (une fois par seconde)
- Pour un modÃ¨le plus Â«Â haut de gammeÂ Â» voir le DHT22


## DHT11 - Librairie
- Le DHT11 utilise son propre protocole de communication
- Pour faciliter son utilisation, on utilise gÃ©nÃ©ralement une librairie
- Nous allons utiliser la librairie Â«Â DHT sensor libraryÂ Â» dâ€™Adafruit
  - [Adafruit](https://www.adafruit.com/) est un fournisseur populaire de composants Ã©lectronique

> *Note :* La vitesse de requÃªte ne doit pas dÃ©passer 1 Hz. Ainsi, on doit attendre au moins 1 seconde Ã  chaque demande

## DHT11 - Branchement et code
Voici une faÃ§on simple d'effectuer le branchement et un exemple de code

<table><tr><td>


```cpp
#include <DHT.h>

// Broche de donnÃ©es
#define DHTPIN 2

// Type de capteur pour la librairie
#define DHTTYPE DHT11   // DHT 11

// DÃ©claration de l'objet
DHT dht(DHTPIN, DHTTYPE);

long currentTime = 0;
long dhtPrevious = 0;
long dhtDelay = 1000;

void setup() {
  Serial.begin(9600);
  Serial.println(F("DHTxx test!"));

  dht.begin(); // Initialisation

  currentTime = millis();
}

void loop() {
  currentTime = millis();

  if (currentTime - dhtPrevious >= dhtDelay) {
    dhtPrevious = currentTime;
    
    float h = dht.readHumidity();
    float t = dht.readTemperature();
    
    // VÃ©rifier si les lectures ont Ã©chouÃ©
    if (isnan(h) || isnan(t)) {
      Serial.println("Erreur de lecture du capteur DHT! On attend 2 secondes.");
      delay(2000); // Attendre 2 secondes avant la prochaine lecture
    } else {
      float humidex = dht.computeHeatIndex(t, h, false);

      Serial.print("HumiditÃ©: ");
      Serial.print(h);
      Serial.print("%  TempÃ©rature: ");
      Serial.print(t);
      Serial.print("Â°C ");
      Serial.print("Humidex : ");
      Serial.print(humidex);
      Serial.println("Â°C ");
    }
  }
}

```

</td>
<td>

![Alt text](img/05_dht11_wiring.png)

</td>
</tr>
</table>

---

# Ã‰cran LCD
- L'Ã©cran LCD permet d'afficher du contenu textuel ou graphique sur l'appareil
- On le retrouve dans plusieurs appareils communs
  - Imprimante, routeur, cafetiÃ¨re, etc.
- Il y a plusieurs types d'Ã©cran LCD
  - Couleur, LCD 1602, 2004, OLED 128x64, etc.

## Dans le kit
- Dans le kit entre nos mains, il y a un Ã©cran LCD 1602
  - 1602 pour 16 caractÃ¨res de large et 2 lignes
- Par dÃ©faut le modÃ¨le fourni a un connecteur en parallÃ¨le
  - Cela occupe beaucoup de broches
  
![Alt text](img/07_lcd_wiring.png)

- Il existe un module qui permet de rÃ©duire le nombre de broches utilisÃ©es
- Il sâ€™agit du module LCD i2c
  - On verra le i2c dans un prochain cours
- Au lieu dâ€™utiliser 7 fils, ce module permet de rÃ©duire Ã  2 fils
  - On ne compte jamais les fils de tension (5v) et de mise Ã  la terre

![Alt text](img/07_lcd_i2c.png)

- Pour brancher le module i2c avec lâ€™Ã©cran, il faut les aligner les broches en parallÃ¨le
- Le port SDA doit aller sur le port SDA (#20)
- Le port SCL doit aller sur le port SCL (#21)

![Alt text](img/07_lcd_pins_aligned.png)

## LCD - Code
Nous allons utiliser la librairie Â«Â LiquidCrystal i2cÂ Â» de **Frank de Brabander**

```cpp
#include <LiquidCrystal_I2C.h>

// Adresse i2c : 0x27
// 16 caractÃ¨res et 2 lignes
LiquidCrystal_I2C lcd(0x27,16,2);  

void setup()
{
  
  lcd.init(); // initialize the lcd              
  // Afficher un message
  lcd.backlight();
  // Positionner le curseur
  lcd.setCursor(0,0);
  // Ã‰criture
  lcd.print("Hello, world!");
  lcd.setCursor(0,1);
  lcd.print("NickB Arduino!");
}

```

----

# Exercices

## Sonar sans librairie
- Reproduisez le code qui est dans la section [Exemple de code](#exemple-de-code)
- Effectuez le branchement adÃ©quat selon le code
- Testez le code

## Sonar avec librairie
- Installer la librairie Â«Â HCSR04Â ultrasonic sensor Â» de gamegine
  - Rechercher Â«Â hcsr04Â Â»
- RÃ©cupÃ©rer lâ€™exemple Â«  HCSR04Â Â»
- Modifier le code selon le branchement actuel
- Tester le code

Le code de l'exemple :

```cpp
#include <HCSR04.h>
// trig, echo
HCSR04 hc(6, 5);
float dist = 0.0;

void setup()
{
    Serial.begin(9600);
}

void loop()
{
    dist = hc.dist();
    Serial.println(dist);
    delay(60);
}

```

## Sonar, DHT11 et LCD
Dans le mÃªme projet :
- Affichez la tempÃ©rature, le taux d'humiditÃ© ainsi que la distance
- Faites basculer l'affichage Ã  toutes les 3 secondes entre la tempÃ©rature, le taux d'humiditÃ© et ensuite la distance

---

# RÃ©fÃ©rences
- [Projet Arduino : Ultrason](https://create.arduino.cc/projecthub/abdularbi17/ultrasonic-sensor-hc-sr04-with-arduino-tutorial-327ff6)
- [Projet Arduino : DHT11](https://create.arduino.cc/projecthub/pibots555/how-to-connect-dht11-sensor-with-arduino-uno-f4d239)

---

- [Retour Ã  la table des matiÃ¨res](#table-des-matiÃ¨res)
- [Retour Ã  la liste des leÃ§ons](../README.md)








